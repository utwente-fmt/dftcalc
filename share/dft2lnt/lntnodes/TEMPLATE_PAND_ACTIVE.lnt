module TEMPLATE_PAND_ACTIVE(TEMPLATE_COMMON) is

	process PAND [FAIL : NAT_CHANNEL, ACTIVATE : NAT_BOOL_CHANNEL] (total : NAT, failed : NAT_ARRAY) is
	var
		nr_failed : NAT,
		done : BOOL,
		never_fail: BOOL,
		nr : NAT,
		last : NAT,
		nr_act: NAT
	in
		done := FALSE;
		nr_failed := 1 of NAT;
		nr_act := 0 of NAT;
		never_fail := FALSE;
		loop
			select

			(*
			 * Listen for any child to be triggered.
			 * If this happens, we add the localNodeID of the failing node to
			 * the list of failed nodes.
			 * The PAnd children should all fail from left to right. Note that
			 * for a PAnd with two children supporting repair, the following
			 * sequence would cause the PAnd to fail:
			 *   - Fail B
			 *   - Fail A
			 *   - Repair B
			 *   - Fail B
			 *)
				FAIL (?nr) where (0 < nr) and (nr <= total) and not(never_fail);
				if(nr == nr_failed) then
					nr_failed:=nr_failed+1
				else
					never_fail:=TRUE
				end if;
				nr := 0

			(*
			 * Checks if all the children have failed and if they failed in
			 * the right order. If this is the case, the PAnd fails and
			 * notifies its parents.
			 *)
			[]
				if (nr_failed > total) and not (done) and not(never_fail) then
					FAIL (!0 of NAT); done := TRUE
				end if

			end select
		end loop
	end var
	end process


end module
